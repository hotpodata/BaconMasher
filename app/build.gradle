apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.google.gms.google-services'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 14
        applicationId "com.hotpodata.baconmasher"
        targetSdkVersion 23
        versionCode 2
        versionName "1.0.1"
        multiDexEnabled true
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "LOGGING_ENABLED", "true"
            buildConfigField "boolean", "IS_DEBUG_BUILD", "true"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            buildConfigField "boolean", "LOGGING_ENABLED", "false"
            buildConfigField "boolean", "IS_DEBUG_BUILD", "false"
        }
    }
    signingConfigs {
        def provDir = System.getenv("BACONMASHER_KEYSTORE_DIRECTORY") ?: ""
        def baconmasherKeystorePass = System.getenv("BACONMASHER_KEYSTORE_PASS") ?: ""
        def baconmasherProPass = System.getenv("BACONMASHER_PRO_PASS") ?: ""
        baconmasherProRelease {
            storeFile file("${provDir}/baconmasher.keystore")
            storePassword baconmasherKeystorePass
            keyAlias "baconmasher-pro-release"
            keyPassword baconmasherProPass
        }

        def baconmasherFreePass = System.getenv("BACONMASHER_FREE_PASS") ?: ""
        baconmasherFreeRelease {
            storeFile file("${provDir}/baconmasher.keystore")
            storePassword baconmasherKeystorePass
            keyAlias "baconmasher-free-release"
            keyPassword baconmasherFreePass
        }
    }
    productFlavors {
        def freeSigning = signingConfigs.baconmasherFreeRelease
        def proSigning = signingConfigs.baconmasherProRelease
        Free {
            applicationId "com.hotpodata.baconmasher.free"
            buildConfigField "boolean", "IS_PRO", "false"
            signingConfig freeSigning
            resValue "string", "app_label", "Baconmasher"
            resValue "string", "file_provider_authorities", "com.hotpodata.baconmasher.free.fileprovider"
        }
        Pro {
            applicationId "com.hotpodata.baconmasher.pro"
            buildConfigField "boolean", "IS_PRO", "true"
            signingConfig proSigning
            resValue "string", "app_label", "Baconmasher Pro"
            resValue "string", "file_provider_authorities", "com.hotpodata.baconmasher.pro.fileprovider"
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}
task switchToFree(type: Copy) {
    description = 'Switches to FREE google-services.json'
    from "src/free"
    include "google-services.json"
    into "."
}

task switchToPro(type: Copy) {
    description = 'Switches to PRO google-services.json'
    from "src/pro"
    include "google-services.json"
    into "."
}
dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:support-v4:23.1.0'
    compile 'com.android.support:appcompat-v7:23.1.0'
    compile 'com.android.support:design:23.1.0'
    compile 'com.android.support:cardview-v7:23.1.0'
    compile 'com.android.support:recyclerview-v7:23.1.0'
    compile 'com.android.support:multidex:1.0.0'
    compile 'com.google.android.gms:play-services:8.3.0'
    compile 'com.google.android.gms:play-services-ads:8.3.0'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile 'com.jakewharton.timber:timber:3.1.0'
    compile 'com.google.android.gms:play-services-analytics:8.3.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'me.grantland:autofittextview:0.2.+'
    compile project(':baconforkotlin')
}
buildscript {
    ext.kotlin_version = '1.0.0-beta-3595'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
repositories {
    mavenCentral()
}

afterEvaluate {
    processFreeDebugGoogleServices.dependsOn switchToFree
    processFreeReleaseGoogleServices.dependsOn switchToFree
    processProDebugGoogleServices.dependsOn switchToPro
    processProReleaseGoogleServices.dependsOn switchToPro
}
